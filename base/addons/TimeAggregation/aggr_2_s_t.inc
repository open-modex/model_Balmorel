* BATINCLUDE aggr_2_s_t.inc:
* This file aggregates a timeseries with:
*        - parameter name (e.g. DE_VAR_T), this is argument %1
*        - one prefixed set (e.g. IA or IR), this is argument %2
*        - seasons (SSS)
*        - time steps (SSS)


SET
         TENSTEPS / STEP01*STEP10 /,

         CURRENT(S,T)
         REMAINING(S,T)
         CURTENSTEPS(TENSTEPS)

         CURRENT_HOUR(SSS,TTT)
         REMAINING_HOURS(SSS,TTT)

;
ALIAS(TENSTEPS,ITENSTEPS);
ALIAS(S, ISALIAS);
ALIAS(T, ITALIAS);

PARAMETER
         VAR_NY(AAA,SSS,TTT)
         IVAR_SUMST(AAA)
         SUMSERIES(SSS,TTT)
         MAXSUMSERIES
         WEIGHTSUMSERIES
         DISTRIBUTION(TENSTEPS)
         CUMDISTRIBUTION(TENSTEPS)
         ASSOCIATION(TENSTEPS,SSS,TTT)
         SUMDISTRIBUTION
         RANDOMORDER(S,T)
         ITER
         ONCE
;

IVAR_SUMST(IA) = SUM((SSS,TTT), WND_VAR_T(IA,SSS,TTT));

SUMSERIES(SSS,TTT) = SUM(IA$IVAR_SUMST(IA), WND_VAR_T(IA,SSS,TTT))/SUM(IA$IVAR_SUMST(IA),1);

WEIGHTSUMSERIES = SUM((SSS,TTT), SUMSERIES(SSS,TTT));

MAXSUMSERIES = SMAX((SSS,TTT),SUMSERIES(SSS,TTT));
* Rescale SERIES:
SUMSERIES(SSS,TTT) = SUMSERIES(SSS,TTT)/MAXSUMSERIES;

DISTRIBUTION(TENSTEPS) = SUM((SSS,TTT)$(  (ord(TENSTEPS)-1)/CARD(TENSTEPS) <= SUMSERIES(SSS,TTT)
                                          and SUMSERIES(SSS,TTT) < ord(TENSTEPS)/CARD(TENSTEPS)),1)/8760;
CUMDISTRIBUTION(TENSTEPS) = SUM(ITENSTEPS$(ORD(ITENSTEPS) <=  ORD(TENSTEPS)), DISTRIBUTION(ITENSTEPS));

SUMDISTRIBUTION = SUM(TENSTEPS,DISTRIBUTION(TENSTEPS));

ASSOCIATION(TENSTEPS,SSS,TTT) = yes$(  (ord(TENSTEPS)-1)/CARD(TENSTEPS) <= SUMSERIES(SSS,TTT)
                                          and SUMSERIES(SSS,TTT) < ord(TENSTEPS)/CARD(TENSTEPS));

*VAR_NY(IA,S,T) =

RANDOMORDER(S,T) = UNIFORM(0,1);

REMAINING(S,T)=yes;
REMAINING_HOURS(SSS,TTT)=yes;
ONCE = 1;


for (ITER = 0 to CARD(S)*CARD(T),
   CURRENT(S,T)=yes$(RANDOMORDER(S,T) = SMIN((ISALIAS,ITALIAS)$REMAINING(ISALIAS,ITALIAS), RANDOMORDER(ISALIAS,ITALIAS)) );
   CURTENSTEPS(TENSTEPS) = yes$((CUMDISTRIBUTION(TENSTEPS-1) <= ITER/(CARD(S)*CARD(T))) and  (ITER/(CARD(S)*CARD(T)) < CUMDISTRIBUTION(TENSTEPS)));
   LOOP((CURTENSTEPS,SSS,TTT)$(REMAINING_HOURS(SSS,TTT) and ASSOCIATION(CURTENSTEPS,SSS,TTT) and ONCE),
         CURRENT_HOUR(SSS,TTT)=yes;

         VAR_NY(IA,S,T)$CURRENT(S,T) = WND_VAR_T(IA,SSS,TTT);

         REMAINING_HOURS(SSS,TTT)$CURRENT_HOUR(SSS,TTT) = no;
         ONCE = 0;
   );
   ONCE = 1;
   REMAINING(S,T)$CURRENT(S,T) = no;
);

* Execute_unload 'results.gdx', DISTRIBUTION, CUMDISTRIBUTION, WND_VAR_T, VAR_NY, SUMSERIES, REMAINING, iter, CURRENT, CURTENSTEPS;

WND_VAR_T(IA,SSS,TTT)=0;
WND_VAR_T(IA,S,T)=VAR_NY(IA,S,T);


$kill VAR_NY
$kill SUMSERIES
$kill MAXSUMSERIES
$kill WEIGHTSUMSERIES
$kill DISTRIBUTION
$kill CUMDISTRIBUTION
$kill ASSOCIATION
$kill SUMDISTRIBUTION
$kill RANDOMORDER
$kill ITER
$kill ONCE
