Equations
   QHYDROGEN_GGETOH2(AAA,G,S,T)       'Hydrogen production - existing electrolysis plant'
   QHYDROGEN_GNGETOH2(AAA,G,S,T)      'Hydrogen production - new electrolysis plant'
   QHYDROGEN_HGETOH2(AAA,G,S,T)       'Heat generated - existing electrolysis plant'
   QHYDROGEN_HNGETOH2(AAA,G,S,T)      'Heat generated - new electrolysis plant'

   QHYDROGEN_GGEHTOH2(AAA,G,S,T)       'Hydrogen production - existing SOEC plant'
   QHYDROGEN_GNGEHTOH2(AAA,G,S,T)      'Hydrogen production - new SOEC plant'
   QHYDROGEN_HGEHTOH2(AAA,G,S,T)       'Heat used - existing SOEC plant'
   QHYDROGEN_HNGEHTOH2(AAA,G,S,T)      'Heat used - new SOEC plant'

   QHYDROGEN_STOVOL(AAA,G,S,T)      'Dynamic equation hydrogen storage - not used in BALBASE3'
   QHYDROGEN_STOVOL_T(AAA,G,S,T)    'Dynamic equation hydrogen storage - used in BALBASE3'
   QHYDROGEN_STOVOL_END(AAA,G,S,T)  'End level of hydrogen storage fixed to value found in yearly simulation - used in BALBASE3'
   QHYDROGEN_STOMAXCON(AAA,G,S,T)   'Maximum hydrogen storage capacity - used in BALBASE1 and BALBASE2'
   QHYDROGEN_STOMAXCON_T(AAA,G,S,T) 'Maximum hydrogen storage capacity - used in BALBASE3'
   QHYDROGEN_STOMAXLOAD(AAA,G,S,T)  'Maximum loading capacity of hydrogen storage'
   QHYDROGEN_STOMAXUNLD(AAA,G,S,T)  'Maximum unloading capacity of hydrogen storage'
   QHYDROGEN_EQ(RRR,S,T)            'Hydrogen production equal demand'
   QHYDROGEN_EQ_AREA(AAA,S,T)       'Hydrogen production equals demand at an areal level'
   QHYDROGEN_GCH4KNT(AAA,G,S,T)       'Hydrogen production of new steam reforming plants restricted by capacity'
;


* Electrolysis plants, existing:

QHYDROGEN_GGETOH2(IAGK_Y(IA,IHYDROGEN_GETOH2),IS3,T) ..

       VGE_T(IA,IHYDROGEN_GETOH2,IS3,T) =E= VHYDROGEN_GH2_T(IA,IHYDROGEN_GETOH2,IS3,T)/GDATA(IHYDROGEN_GETOH2,'GDFE');

* Electrolysis plants, new:

QHYDROGEN_GNGETOH2(IAGKN(IA,IHYDROGEN_GETOH2),IS3,T) ..

       VGEN_T(IA,IHYDROGEN_GETOH2,IS3,T)=E= VHYDROGEN_GH2N_T(IA,IHYDROGEN_GETOH2,IS3,T)/GDATA(IHYDROGEN_GETOH2,'GDFE');

QHYDROGEN_HGETOH2(IAGK_Y(IA,IHYDROGEN_GETOH2),IS3,T)..

       VGE_T(IA,IHYDROGEN_GETOH2,IS3,T) =E= VGH_T(IA,IHYDROGEN_GETOH2,IS3,T)*GDATA(IHYDROGEN_GETOH2,'GDCB');

QHYDROGEN_HNGETOH2(IAGKN(IA,IHYDROGEN_GETOH2),IS3,T)..

       VGEN_T(IA,IHYDROGEN_GETOH2,IS3,T)=E= VGHN_T(IA,IHYDROGEN_GETOH2,IS3,T)*GDATA(IHYDROGEN_GETOH2,'GDCB');

* SOEC plants, existing:

QHYDROGEN_GGEHTOH2(IAGK_Y(IA,IHYDROGEN_GEHTOH2),IS3,T) ..

       VGE_T(IA,IHYDROGEN_GEHTOH2,IS3,T) =E= VHYDROGEN_GH2_T(IA,IHYDROGEN_GEHTOH2,IS3,T)/GDATA(IHYDROGEN_GEHTOH2,'GDFE');

* SOEC plants, new:

QHYDROGEN_GNGEHTOH2(IAGKN(IA,IHYDROGEN_GEHTOH2),IS3,T) ..

       VGEN_T(IA,IHYDROGEN_GEHTOH2,IS3,T)=E= VHYDROGEN_GH2N_T(IA,IHYDROGEN_GEHTOH2,IS3,T)/GDATA(IHYDROGEN_GEHTOH2,'GDFE');

QHYDROGEN_HGEHTOH2(IAGK_Y(IA,IHYDROGEN_GEHTOH2),IS3,T)..

       VGE_T(IA,IHYDROGEN_GEHTOH2,IS3,T) =E= VGH_T(IA,IHYDROGEN_GEHTOH2,IS3,T)*GDATA(IHYDROGEN_GEHTOH2,'GDCB');

QHYDROGEN_HNGEHTOH2(IAGKN(IA,IHYDROGEN_GEHTOH2),IS3,T)..

       VGEN_T(IA,IHYDROGEN_GEHTOH2,IS3,T)=E= VGHN_T(IA,IHYDROGEN_GEHTOH2,IS3,T)*GDATA(IHYDROGEN_GEHTOH2,'GDCB');

* Hydrogen production from steam reforming restricted by capacity
QHYDROGEN_GCH4KNT(IAGKN(IA,IHYDROGEN_GCH4TOH2),IS3,T) ..
  VGKN(IA,IHYDROGEN_GCH4TOH2)*GKRATE(IA,IHYDROGEN_GCH4TOH2,IS3)
   =G= VHYDROGEN_GH2N_T(IA,IHYDROGEN_GCH4TOH2,IS3,T)
;


* Hydrogen gas storage, dynamic balance for weekly time horizon:
* Start and end filling degree in each week fixed to values obtained
* from yearly simulation

QHYDROGEN_STOVOL_T(IAGK_Y(IA,IHYDROGEN_GH2STO),IS3,T)$(ORD(T)< CARD(T))..

    VHYDROGEN_STOVOL_T(IA,IHYDROGEN_GH2STO,IS3,T+1) =E= VHYDROGEN_STOVOL_T(IA,IHYDROGEN_GH2STO,IS3,T)
*(1-GDATA(IHYDROGEN_GH2STO,'GDSTOLOSS'))
  + VHYDROGEN_STOLOADT(IA,IHYDROGEN_GH2STO,IS3,T)*(1-GDATA(IHYDROGEN_GH2STO,'GDLOADLOSS'))- VHYDROGEN_GH2_T(IA,IHYDROGEN_GH2STO,IS3,T)
;

* End level of hydrogen storage in season S fixed to start level in season S+1
* Values found in yearly simulation

QHYDROGEN_STOVOL_END(IAGK_Y(IA,IHYDROGEN_GH2STO),IS3,T)$(ORD(T)=CARD(T))..
   VHYDROGEN_STOVOL_T(IA,IHYDROGEN_GH2STO,IS3,T)
*(1-GDATA(IHYDROGEN_GH2STO,'GDSTOLOSS'))
  + VHYDROGEN_STOLOADT(IA,IHYDROGEN_GH2STO,IS3,T)*(1-GDATA(IHYDROGEN_GH2STO,'GDLOADLOSS'))- VHYDROGEN_GH2_T(IA,IHYDROGEN_GH2STO,IS3,T)=E=IHYDROGEN_STOVOL_END(IA,IHYDROGEN_GH2STO,IS3)
;

* Maximum content, hydrogen gas storage - used in bb1 and bb2:
*Hydrogen storage is modelled as a inter-seasonal storage that can be used with a T resolutions

QHYDROGEN_STOMAXCON(IA,IHYDROGEN_GH2STO,S,T)$(IAGK_Y(IA,IHYDROGEN_GH2STO) OR IAGKN(IA,IHYDROGEN_GH2STO))..
   (IGKVACCTOY(IA,IHYDROGEN_GH2STO)+IGKFX_Y(IA,IHYDROGEN_GH2STO))$IAGK_Y(IA,IHYDROGEN_GH2STO)
   + VGKN(IA,IHYDROGEN_GH2STO)$IAGKN(IA,IHYDROGEN_GH2STO)
    =G= VHYDROGEN_STOVOL_T(IA,IHYDROGEN_GH2STO,S,T)
;

* Maximum content, hydrogen gas storage - used in bb3:

QHYDROGEN_STOMAXCON_T(IA,IHYDROGEN_GH2STO,IS3,T)$IAGK_Y(IA,IHYDROGEN_GH2STO)..
   IGKVACCTOY(IA,IHYDROGEN_GH2STO)+IGKFX_Y(IA,IHYDROGEN_GH2STO) =G= VHYDROGEN_STOVOL_T(IA,IHYDROGEN_GH2STO,IS3,T)
;

* Maximum loading, hydrogen gas storage:
QHYDROGEN_STOMAXLOAD(IA,IHYDROGEN_GH2STO,IS3,T)$(IAGK_Y(IA,IHYDROGEN_GH2STO) OR IAGKN(IA,IHYDROGEN_GH2STO))..
   ((IGKVACCTOY(IA,IHYDROGEN_GH2STO)+IGKFX_Y(IA,IHYDROGEN_GH2STO))/GDATA(IHYDROGEN_GH2STO,'GDSTOHLOAD'))$IAGK_Y(IA,IHYDROGEN_GH2STO)
   + (VGKN(IA,IHYDROGEN_GH2STO)/GDATA(IHYDROGEN_GH2STO,'GDSTOHLOAD'))$IAGKN(IA,IHYDROGEN_GH2STO)
    =G= VHYDROGEN_STOLOADT(IA,IHYDROGEN_GH2STO,IS3,T)
;

* Maximum unloading, hydrogen gas storage:
QHYDROGEN_STOMAXUNLD(IA,IHYDROGEN_GH2STO,IS3,T)$(IAGK_Y(IA,IHYDROGEN_GH2STO) OR IAGKN(IA,IHYDROGEN_GH2STO))..
   ((IGKVACCTOY(IA,IHYDROGEN_GH2STO)+IGKFX_Y(IA,IHYDROGEN_GH2STO))/GDATA(IHYDROGEN_GH2STO,'GDSTOHUNLD'))$IAGK_Y(IA,IHYDROGEN_GH2STO)
   + (VGKN(IA,IHYDROGEN_GH2STO)/GDATA(IHYDROGEN_GH2STO,'GDSTOHUNLD'))$IAGKN(IA,IHYDROGEN_GH2STO)
    =G=
   VHYDROGEN_GH2_T(IA,IHYDROGEN_GH2STO,IS3,T)$IAGK_Y(IA,IHYDROGEN_GH2STO)+
   VHYDROGEN_GH2N_T(IA,IHYDROGEN_GH2STO,IS3,T)$IAGKN(IA,IHYDROGEN_GH2STO)
;

* Hydrogen gas storage, dynamic balance for yearly time horizon:
* Hydrogen gas storage modelled as start level being equal to end level for each year

QHYDROGEN_STOVOL(IA,IHYDROGEN_GH2STO,S,T)$(IAGK_Y(IA,IHYDROGEN_GH2STO) OR IAGKN(IA,IHYDROGEN_GH2STO))..

    VHYDROGEN_STOVOL_T(IA,IHYDROGEN_GH2STO,S,T+1)
    +SUM(ITALIAS$(ORD(ITALIAS)=1),VHYDROGEN_STOVOL_T(IA,IHYDROGEN_GH2STO,S++1,ITALIAS)$(ORD(T)=CARD(T)))
    =E=
     VHYDROGEN_STOVOL_T(IA,IHYDROGEN_GH2STO,S,T)*(1-GDATA(IHYDROGEN_GH2STO,'GDSTOLOSS'))
    +(VHYDROGEN_STOLOADT(IA,IHYDROGEN_GH2STO,S,T)*(1-GDATA(IHYDROGEN_GH2STO,'GDLOADLOSS'))
    -((VHYDROGEN_GH2_T(IA,IHYDROGEN_GH2STO,S,T))$IAGK_Y(IA,IHYDROGEN_GH2STO)+(VHYDROGEN_GH2N_T(IA,IHYDROGEN_GH2STO,S,T))$IAGKN(IA,IHYDROGEN_GH2STO)))
    *(IHOURSINST(S,T)*(CARD(S)/CARD(SSS))*(1/CYCLESINS(S)));
;

* Hydrogen production equal demand on region level:
* Prod + OutputStorage - InputStorage = DemandTransport + DemandPower+DemandBioRefinery (other exogenous demand)

QHYDROGEN_EQ(IR,IS3,T)..
   SUM(IAGK_Y(IA,IHYDROGEN_GETOH2)$(RRRAAA(IR,IA)),VHYDROGEN_GH2_T(IA,IHYDROGEN_GETOH2,IS3,T)) +
   SUM(IAGKN(IA,IHYDROGEN_GETOH2) $(RRRAAA(IR,IA)),VHYDROGEN_GH2N_T(IA,IHYDROGEN_GETOH2,IS3,T)) +
   SUM(IAGK_Y(IA,IHYDROGEN_GCH4TOH2)$(RRRAAA(IR,IA)),VHYDROGEN_GH2_T(IA,IHYDROGEN_GCH4TOH2,IS3,T)) +
   SUM(IAGKN(IA,IHYDROGEN_GCH4TOH2) $(RRRAAA(IR,IA)),VHYDROGEN_GH2N_T(IA,IHYDROGEN_GCH4TOH2,IS3,T)) +
   SUM(IAGK_Y(IA,IHYDROGEN_GH2STO)$(RRRAAA(IR,IA)),VHYDROGEN_GH2_T(IA,IHYDROGEN_GH2STO,IS3,T)) +
   SUM(IAGKN(IA,IHYDROGEN_GH2STO) $(RRRAAA(IR,IA)),VHYDROGEN_GH2N_T(IA,IHYDROGEN_GH2STO,IS3,T)) +
   SUM(IAGK_Y(IA,IHYDROGEN_GEHTOH2)$(RRRAAA(IR,IA)),VHYDROGEN_GH2_T(IA,IHYDROGEN_GEHTOH2,IS3,T)) +
   SUM(IAGKN(IA,IHYDROGEN_GEHTOH2) $(RRRAAA(IR,IA)),VHYDROGEN_GH2N_T(IA,IHYDROGEN_GEHTOH2,IS3,T))
   - SUM((IA,IHYDROGEN_GH2STO)$(RRRAAA(IR,IA) AND (IAGK_Y(IA,IHYDROGEN_GH2STO) OR IAGKN(IA,IHYDROGEN_GH2STO))),VHYDROGEN_STOLOADT(IA,IHYDROGEN_GH2STO,IS3,T))
   =E=
   IHYDROGEN_DH2_REGION_T_Y(IR,IS3,T)+ SUM(IA$RRRAAA(IR,IA),IHYDROGEN_DH2_AREA_T_Y(IA,IS3,T))
   + SUM(IAGK_Y(IA,IHYDROGEN_GH2FUEL) $(RRRAAA(IR,IA)),VGE_T(IA,IHYDROGEN_GH2FUEL,IS3,T)/GDATA(IHYDROGEN_GH2FUEL,'GDFE'))
   + SUM(IAGKN(IA,IHYDROGEN_GH2FUEL) $(RRRAAA(IR,IA)),VGEN_T(IA,IHYDROGEN_GH2FUEL,IS3,T)/GDATA(IHYDROGEN_GH2FUEL,'GDFE'))
   + SUM(IAGK_Y(IA,IHYDROGEN_GH2UP) $(RRRAAA(IR,IA)),HYDROGEN_H2TOF(IHYDROGEN_GH2UP)*VGF_T(IA,IHYDROGEN_GH2UP,IS3,T))
   + SUM(IAGKN(IA,IHYDROGEN_GH2UP) $(RRRAAA(IR,IA)),HYDROGEN_H2TOF(IHYDROGEN_GH2UP)*VGFN_T(IA,IHYDROGEN_GH2UP,IS3,T))
*        + SUM(HYDROGEN_DH2F_U,VHYDROGEN_DH2F_T(IR,IS3,T,HYDROGEN_DH2F_U) )
*        - SUM(HYDROGEN_DH2F_D,VHYDROGEN_DH2F_T(IR,IS3,T,HYDROGEN_DH2F_D) )
         - VHYDROGEN_QH2EQ(IR,IS3,T,'IMINUS') + VHYDROGEN_QH2EQ(IR,IS3,T,'IPLUS')
;


* Hydrogen production equal demand on area level:
* Prod + OutputStorage - InputStorage = DemandTransport + DemandPower

QHYDROGEN_EQ_AREA(IA,IS3,T)$(IHYDROGEN_DH2_AREA_T_Y(IA,IS3,T))..
   SUM(IHYDROGEN_GETOH2$IAGK_Y(IA,IHYDROGEN_GETOH2),VHYDROGEN_GH2_T(IA,IHYDROGEN_GETOH2,IS3,T)) +
   SUM(IHYDROGEN_GETOH2$IAGKN(IA,IHYDROGEN_GETOH2),VHYDROGEN_GH2N_T(IA,IHYDROGEN_GETOH2,IS3,T)) +
   SUM(IHYDROGEN_GEHTOH2$IAGK_Y(IA,IHYDROGEN_GEHTOH2),VHYDROGEN_GH2_T(IA,IHYDROGEN_GEHTOH2,IS3,T)) +
   SUM(IHYDROGEN_GEHTOH2$IAGKN(IA,IHYDROGEN_GEHTOH2),VHYDROGEN_GH2N_T(IA,IHYDROGEN_GEHTOH2,IS3,T)) +
   SUM(IHYDROGEN_GH2STO$IAGK_Y(IA,IHYDROGEN_GH2STO) ,VHYDROGEN_GH2_T(IA,IHYDROGEN_GH2STO,IS3,T)) +
   SUM(IHYDROGEN_GH2STO$IAGKN(IA,IHYDROGEN_GH2STO),VHYDROGEN_GH2N_T(IA,IHYDROGEN_GH2STO,IS3,T))
   - SUM(IHYDROGEN_GH2STO$(IAGK_Y(IA,IHYDROGEN_GH2STO) OR IAGKN(IA,IHYDROGEN_GH2STO)),VHYDROGEN_STOLOADT(IA,IHYDROGEN_GH2STO,IS3,T))
   =G=
   IHYDROGEN_DH2_AREA_T_Y(IA,IS3,T)
*        + SUM(HYDROGEN_DH2F_U,VHYDROGEN_DH2F_T(IA,IS3,T,HYDROGEN_DH2F_U) )
*        - SUM(HYDROGEN_DH2F_D,VHYDROGEN_DH2F_T(IA,IS3,T,HYDROGEN_DH2F_D) )
         - VHYDROGEN_QH2EQ(IA,IS3,T,'IMINUS') + VHYDROGEN_QH2EQ(IA,IS3,T,'IPLUS')
;

