Equations
   QHYDROGEN_GGETOH2(Y,AAA,G,S,T)       'Hydrogen production - existing electrolysis plant'
   QHYDROGEN_HGETOH2(Y,AAA,G,S,T)       'Heat generated - existing electrolysis plant'

*Ida additions
   QHYDROGEN_GGEHTOH2(Y,AAA,G,S,T)       'Hydrogen production - existing SOEC plant'
   QHYDROGEN_HGEHTOH2(Y,AAA,G,S,T)       'Heat used - existing SOEC plant'

   QHYDROGEN_GCH4KT(Y,AAA,G,S,T)       'Hydrogen production of steam reforming plants restricted by capacity'

   QHYDROGEN_STOVOL(Y,AAA,G,S,T)      'Dynamic equation hydrogen storage'
   QHYDROGEN_STOMAXCON(Y,AAA,G,S,T)   'Maximum hydrogen storage capacity'
   QHYDROGEN_STOMAXLOAD(Y,AAA,G,S,T)  'Maximum loading capacity of hydrogen storage'
   QHYDROGEN_STOMAXUNLD(Y,AAA,G,S,T)  'Maximum unloading capacity of hydrogen storage'
   QHYDROGEN_EQ(Y,RRR,S,T)            'Hydrogen production equal demand'
   QHYDROGEN_EQ_AREA(Y,AAA,S,T)       'Hydrogen production equals demand at an areal level'
;


* Electrolysis plants:

QHYDROGEN_GGETOH2(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GETOH2),IS3,T) ..

       VGE_T(IY411,IA,IHYDROGEN_GETOH2,IS3,T) =E= VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GETOH2,IS3,T)/GDATA(IHYDROGEN_GETOH2,'GDFE');


QHYDROGEN_HGETOH2(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GETOH2),IS3,T)..

       VGE_T(IY411,IA,IHYDROGEN_GETOH2,IS3,T) =E= VGH_T(IY411,IA,IHYDROGEN_GETOH2,IS3,T)*GDATA(IHYDROGEN_GETOH2,'GDCB');

* SOEC plants, existing:

QHYDROGEN_GGEHTOH2(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GEHTOH2),IS3,T) ..

       VGE_T(IY411,IA,IHYDROGEN_GEHTOH2,IS3,T) =E= VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GEHTOH2,IS3,T)/GDATA(IHYDROGEN_GEHTOH2,'GDFE');

QHYDROGEN_HGEHTOH2(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GEHTOH2),IS3,T)..

       VGE_T(IY411,IA,IHYDROGEN_GEHTOH2,IS3,T) =E= VGH_T(IY411,IA,IHYDROGEN_GEHTOH2,IS3,T)*GDATA(IHYDROGEN_GEHTOH2,'GDCB');

* Hydrogen gas storage, dynamic balance for weekly time horizon:
* Start and end filling degree in each week fixed to values obtained
* from yearly simulation


* Hydrogen production equal demand on region level:
* Prod + OutputStorage - InputStorage = DemandTransport + DemandPower+DemandBioRefinery (other exogenous demand)

QHYDROGEN_EQ(IY411,IR,IS3,T)..
   SUM(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GETOH2)$(RRRAAA(IR,IA)),VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GETOH2,IS3,T)) +
   SUM(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GCH4TOH2)$(RRRAAA(IR,IA)),VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GCH4TOH2,IS3,T)) +
   SUM(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2STO)$(RRRAAA(IR,IA)),VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GH2STO,IS3,T)) +
*Ida addition
   SUM(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GEHTOH2)$(RRRAAA(IR,IA)),VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GEHTOH2,IS3,T))
   - SUM((IA,IHYDROGEN_GH2STO)$(RRRAAA(IR,IA) AND (IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2STO))),VHYDROGEN_STOLOADT(IY411,IA,IHYDROGEN_GH2STO,IS3,T))
   =E=
   IHYDROGEN_DH2_REGION_T_Y(IY411,IR,IS3,T)+
   SUM(IA$RRRAAA(IR,IA),IHYDROGEN_DH2_AREA_T_Y(IY411,IA,IS3,T))
   + SUM(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2FUEL) $(RRRAAA(IR,IA)),VGE_T(IY411,IA,IHYDROGEN_GH2FUEL,IS3,T)/GDATA(IHYDROGEN_GH2FUEL,'GDFE'))
*IDA new additions
   + SUM(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2UP) $(RRRAAA(IR,IA)),HYDROGEN_H2TOF(IHYDROGEN_GH2UP)*VGF_T(IY411,IA,IHYDROGEN_GH2UP,IS3,T))
         - VHYDROGEN_QH2EQ(IY411,IR,IS3,T,'IMINUS') + VHYDROGEN_QH2EQ(IY411,IR,IS3,T,'IPLUS')
;

* Hydrogen production equal demand on area level:
* Prod + OutputStorage - InputStorage = DemandTransport + DemandPower

QHYDROGEN_EQ_AREA(IY411,IA,IS3,T)$(IHYDROGEN_DH2_AREA_T_Y(IY411,IA,IS3,T))..
   SUM(IHYDROGEN_GETOH2$IAGK_HASORPOT(IY411,IA,IHYDROGEN_GETOH2),VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GETOH2,IS3,T)) +
*Ida addition
   SUM(IHYDROGEN_GEHTOH2$IAGK_HASORPOT(IY411,IA,IHYDROGEN_GEHTOH2),VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GEHTOH2,IS3,T)) +
   SUM(IHYDROGEN_GH2STO$IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2STO) ,VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GH2STO,IS3,T))
   - SUM(IHYDROGEN_GH2STO$(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2STO)),VHYDROGEN_STOLOADT(IY411,IA,IHYDROGEN_GH2STO,IS3,T))
   =G=
   IHYDROGEN_DH2_AREA_T_Y(IY411,IA,IS3,T)
         - VHYDROGEN_QH2EQ(IY411,IA,IS3,T,'IMINUS') + VHYDROGEN_QH2EQ(IY411,IA,IS3,T,'IPLUS')
;

* Hydrogen production from steam reforming restricted by capacity
QHYDROGEN_GCH4KT(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GCH4TOH2),IS3,T) ..
  IAGK_HASORPOT(IY411,IA,IHYDROGEN_GCH4TOH2)*GKRATE(IA,IHYDROGEN_GCH4TOH2,IS3)
   =G= VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GCH4TOH2,IS3,T)
;


QHYDROGEN_STOVOL(IY411,IA,IHYDROGEN_GH2STO,S,T)$(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2STO))..

    VHYDROGEN_STOVOL_T(IY411,IA,IHYDROGEN_GH2STO,S,T+1)
    +SUM(ITALIAS$(ORD(ITALIAS)=1),VHYDROGEN_STOVOL_T(IY411,IA,IHYDROGEN_GH2STO,S++1,ITALIAS)$(ORD(T)=CARD(T)))
    =E=
     VHYDROGEN_STOVOL_T(IY411,IA,IHYDROGEN_GH2STO,S,T)*(1-GDATA(IHYDROGEN_GH2STO,'GDSTOLOSS'))
    +(
      VHYDROGEN_STOLOADT(IY411,IA,IHYDROGEN_GH2STO,S,T)*(1-GDATA(IHYDROGEN_GH2STO,'GDLOADLOSS'))
      -VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GH2STO,S,T)
      )*CHRONOHOUR(S,T);
;

*Hydrogen storage is modelled as a inter-seasonal storage that can be used with a T resolutions
QHYDROGEN_STOMAXCON(IY411,IA,IHYDROGEN_GH2STO,S,T)$(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2STO))..
     GKFX(IY411,IA,IHYDROGEN_GH2STO)
   + VGKNACCUMNET(IY411,IA,IHYDROGEN_GH2STO)$SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IHYDROGEN_GH2STO))
    =G=
   VHYDROGEN_STOVOL_T(IY411,IA,IHYDROGEN_GH2STO,S,T)
;

* Maximum loading, hydrogen gas storage:
QHYDROGEN_STOMAXLOAD(IY411,IA,IHYDROGEN_GH2STO,IS3,T)$(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2STO))..
   (   GKFX(IY411,IA,IHYDROGEN_GH2STO)/GDATA(IHYDROGEN_GH2STO,'GDSTOHLOAD')
   + VGKNACCUMNET(IY411,IA,IHYDROGEN_GH2STO)/GDATA(IHYDROGEN_GH2STO,'GDSTOHLOAD')$SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IHYDROGEN_GH2STO))
   ) *(1$(NOT IGKRATE(IA,IHYDROGEN_GH2STO,IS3,T)) + IGKRATE(IA,IHYDROGEN_GH2STO,IS3,T))
    =G= VHYDROGEN_STOLOADT(IY411,IA,IHYDROGEN_GH2STO,IS3,T)
;

* Maximum unloading, hydrogen gas storage:
QHYDROGEN_STOMAXUNLD(IY411,IA,IHYDROGEN_GH2STO,IS3,T)$(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2STO))..
   (   GKFX(IY411,IA,IHYDROGEN_GH2STO)/GDATA(IHYDROGEN_GH2STO,'GDSTOHUNLD')
   + VGKNACCUMNET(IY411,IA,IHYDROGEN_GH2STO)/GDATA(IHYDROGEN_GH2STO,'GDSTOHUNLD')$SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IHYDROGEN_GH2STO))
   ) *(1$(NOT IGKRATE(IA,IHYDROGEN_GH2STO,IS3,T)) + IGKRATE(IA,IHYDROGEN_GH2STO,IS3,T)) 
    =G=
   VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GH2STO,IS3,T)
;
