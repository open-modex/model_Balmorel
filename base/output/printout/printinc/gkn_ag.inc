* File gn_ag.inc, for the Balmorel model.
* Created 2008xxxx(hr). Last revised 200901XX(hr), 20090420(hr).


* The file will print new investments in production capacities by area and technology.
* Includes handling of addon AGKNDISC.


PUT gkn_ag;

* First print headings:
if ((gkn_ag.pw LT 32000),
gkn_ag.pw = 32000;
PUT "Version:              " SYSTEM.TITLE /;
PUT "File name:            gkn_ag.out" /;
PUT "Time-stamp:          '" SYSTEM.DATE " " SYSTEM.TIME "'" /;
PUT "Case-identification: '" OCASEID.TS "'" //;

$ifi %BB2%==yes  PUT "New investments in production capacities by area and technology as generated by model Balbase2" /;
$ifi %BB4%==yes  PUT "New investments in production capacities by area and technology as generated by model Balbase4" /;
PUT "Also annual investment cost (per MW and total) are given."/;
PUT "Units: MW and ?? ." //;

$ifi %AGKNDISC%==yes PUT "Addon AGKNDISC is active with the following areas for possible location of discrete capacity investments in technologies:"/;
$ifi %AGKNDISC%==yes LOOP((IA,G)$IAGKNDISCAG(IA,G), PUT IA.TL:32  G.TL:32 /);
$ifi %AGKNDISC%==yes PUT / "New investments in production capacities by area and technology." /; PUT "Units: MW." //;
);
* End of headings.


$ifi %BB4%==yes $goto label_gknag_label1

PUT / Y.tl /;
LOOP(IA,
LOOP(IGKFIND$VGKN.L(IA,IGKFIND),
*LOOP(IGKFIND/*$VGKN.L(IA,IGKFIND)*/,

PUT IA.TL:18  IGKFIND.TL:18     VGKN.L(IA,IGKFIND):18:2      GDATA(IGKFIND,'GDINVCOST0'):18:6   (VGKN.L(IA,IGKFIND)*GDATA(IGKFIND,'GDINVCOST0') ):18:2  ;
$ifi %AGKNDISC%==yes   IF(AGKNDISCAG(IA,IGKFIND), PUT "   (discrete size) ";);
PUT  /;

));

$label label_gknag_label1

$ifi not %BB4%==yes $goto label_gknag_label2

PUT / "Model Balbase4 over years "; LOOP(IY411, PUT "  " IY411.TL:4 ;); PUT ":"/;

LOOP(IY411,

PUT / IY411.tl /;
LOOP(IA,
LOOP(IGKFIND$VGKN.L(IY411,IA,IGKFIND),
*LOOP(IGKFIND/*$VGKN.L(IY411,IA,IGKFIND)*/,

PUT IA.TL:18  IGKFIND.TL:18     VGKN.L(IY411,IA,IGKFIND):18:2      GDATA(IGKFIND,'GDINVCOST0'):18:6   (VGKN.L(IY411,IA,IGKFIND)*GDATA(IGKFIND,'GDINVCOST0') ):18:2  ;
$ifi %AGKNDISC%==yes   IF(AGKNDISCAG(IA,IGKFIND), PUT "   (discrete size) ";);
PUT  /;

));

);

$label label_gknag_label2
